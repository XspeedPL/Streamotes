plugins {
	alias libs.plugins.fabric.loom
	alias libs.plugins.architectury
}

base {
	archivesName = project.archives_base_name
}

architectury {
	minecraft = libs.versions.minecraft1.'20'.'1'.get()
	platformSetupLoomIde()
	fabric()
}

version = "${project.version}+${libs.versions.minecraft1.'20'.'1'.get()}"
group = project.maven_group

repositories {
	maven { url "https://maven.isxander.dev/releases" }
	maven { url "https://maven.terraformersmc.com" }
}

dependencies {
	minecraft libs.minecraft1.'20'.'1'
	mappings loom.officialMojangMappings()

	modImplementation libs.fabric.loader
	modImplementation libs.fabric.api1.'20'.'1'
	modImplementation libs.modmenu1.'20'.'1'
	modImplementation libs.yacl1.'20'.'1'
}

loom {
	accessWidenerPath = file("src/main/resources/streamotes.classtweaker")
}

processResources {
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
	filesMatching("fabric.mod.json") {
		expand "mod_version": version,
			"minecraft_version": ">=1.20.1 <1.20.5-",
			"loader_version": ">=" + libs.versions.fabric.loader.get()
	}
	filesMatching("streamotes.mixins.json") {
		expand "java_version": "JAVA_17"
	}
}

sourceSets.main {
	java {
		srcDirs "src/main/java", "../fabricbase/src/main/java",
			"../fabricbase/src/pre1_21_2/java"
		exclude "**/JsonPayload.java"
	}
	resources {
		srcDirs "src/main/resources", "../fabricbase/src/main/resources"
	}
}

tasks.withType(JavaCompile).configureEach {
	options.release.set(17)
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
	toolchain.languageVersion = JavaLanguageVersion.of(17)
}
