plugins {
	alias libs.plugins.fabric.loom
}

base {
	archivesName = project.archives_base_name
}

version = "${project.version}+${libs.versions.minecraft1194.get()}"
group = project.maven_group

repositories {
	maven { url "https://maven.isxander.dev/releases" }
	maven { url "https://maven.terraformersmc.com" }
}

configurations.configureEach {
	resolutionStrategy {
		force 'com.twelvemonkeys.common:common-lang:3.10.0'
		force 'com.twelvemonkeys.common:common-image:3.10.0'
		force 'com.twelvemonkeys.common:common-io:3.10.0'
		force 'com.twelvemonkeys.imageio:imageio-core:3.10.0'
		force 'com.twelvemonkeys.imageio:imageio-metadata:3.10.0'
		force 'com.twelvemonkeys.imageio:imageio-webp:3.10.0'
	}
}

dependencies {
	minecraft libs.minecraft1194
	mappings variantOf(libs.fabric.mappings1194) { classifier 'v2' }

	modImplementation libs.fabric.loader
	modImplementation libs.fabric.api1194
	modImplementation libs.modmenu1194
	modImplementation libs.yacl1194
}

processResources {
	filesMatching("fabric.mod.json") {
		expand "mod_version": version,
			"minecraft_version": "1.19.4",
			"loader_version": ">=" + libs.versions.fabric.loader.get()
	}
	filesMatching("streamotes.mixins.json") {
		expand "java_version": "JAVA_17"
	}
}

sourceSets.main {
	java {
		srcDirs "src/main/java", "../fabricbase/src/main/java",
			"../fabricbase/src/pre1212/java"
		exclude "**/JsonPayload.java"
	}
	resources {
		srcDir "../fabricbase/src/main/resources"
	}
}

tasks.withType(JavaCompile).configureEach {
	options.release.set(17)
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
	toolchain.languageVersion = JavaLanguageVersion.of(17)
}
