plugins {
	alias libs.plugins.fabric.loom
	alias libs.plugins.architectury
}

base {
	archivesName = project.archives_base_name
}

architectury {
	minecraft = libs.versions.minecraft1201.get()
	platformSetupLoomIde()
	fabric()
}

version = "${project.version}+${libs.versions.minecraft1201.get()}"
group = project.maven_group

repositories {
	maven { url "https://maven.isxander.dev/releases" }
	maven { url "https://maven.terraformersmc.com" }
}

dependencies {
	minecraft libs.minecraft1201
	mappings loom.officialMojangMappings()

	modImplementation libs.fabric.loader
	modImplementation libs.fabric.api1201
	modImplementation libs.modmenu1201
	modImplementation libs.yacl1201
}

loom {
	accessWidenerPath = file("../fabricbase/src/main/resources/streamotes.accesswidener")
}

processResources {
	filesMatching("fabric.mod.json") {
		expand "mod_version": version,
			"minecraft_version": ">=1.20.1 <1.20.5-",
			"loader_version": ">=" + libs.versions.fabric.loader.get()
	}
	filesMatching("streamotes.mixins.json") {
		expand "java_version": "JAVA_17"
	}
}

sourceSets.main {
	java {
		srcDirs "src/main/java", "../fabricbase/src/main/java",
			"../fabricbase/src/pre1212/java"
		exclude "**/JsonPayload.java"
	}
	resources {
		srcDir "../fabricbase/src/main/resources"
	}
}

tasks.withType(JavaCompile).configureEach {
	options.release.set(17)
}

java {
	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
	toolchain.languageVersion = JavaLanguageVersion.of(17)
}
